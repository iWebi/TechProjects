
import org.openqa.selenium.phantomjs.PhantomJSDriver
import org.openqa.selenium.phantomjs.PhantomJSDriverService
import org.openqa.selenium.remote.DesiredCapabilities

apply plugin: "groovy"
apply plugin: "idea"

repositories {
	mavenCentral()
}

// The drivers we want to use
ext.drivers = ["firefox", "chrome", "phantom"]

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.github.detro.ghostdriver:phantomjsdriver:1.1.0'
	}
}

dependencies {
	def gebVersion = "0.9.2"
	def seleniumVersion = "2.39.0"

	// If using Spock, need to depend on geb-spock
	compile "org.gebish:geb-spock:$gebVersion"
	compile "org.spockframework:spock-core:0.7-groovy-2.0"
	compile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
	compile "org.hibernate:hibernate-annotations:3.4.0.GA"
	compile "com.h2database:h2:1.3.174"
	compile 'org.slf4j:slf4j-log4j12:1.6.6'
	compile 'org.javassist:javassist:3.15.0-GA'
	compile 'org.apache.ws.commons.axiom:axiom-api:1.2.12'
	compile 'javax.mail:mail:1.4.1'
	compile 'org.apache.directory.studio:org.apache.commons.io:2.4'

	// If using JUnit, need to depend on geb-junit (3 or 4)
	compile "org.gebish:geb-junit4:$gebVersion"
	compile("com.github.detro.ghostdriver:phantomjsdriver:1.1.0") {
		transitive = false
	}
}

drivers.each { driver ->
	task "${driver}Test"(type: Test) {
		testReportDir = reporting.file("$name/tests")
		testResultsDir = file("$buildDir/test-results/$name")

		systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
		systemProperty "geb.env", driver

		// If you wanted to set the baseUrl in your build…
		// systemProperty "geb.build.baseUrl", "http://myapp.com"
	}
}

test {
	dependsOn drivers.collect { tasks["${it}Test"] }
	enabled = false
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.8'
}
